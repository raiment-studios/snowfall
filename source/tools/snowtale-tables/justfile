###############################################################################
# Preamble
###############################################################################

default:
  @just --list

###############################################################################
# Core commands
###############################################################################

ensure:
    cp -Rf $MONOREPO_ROOT/.vscode/ .vscode/
    @which mprocs > /dev/null || cargo binstall --root $(MONOREPO_ROOT)/bin/cargo mprocs -y
    npm install

build: ensure
    just _build-bundle ./src/main.tsx ./dist/main.bundle.js
    cp -f ./src/index.html ./dist/index.html

dev: build
    mprocs \
        --names "client,server" \
        "just dev-watch-build" \
        "just run"

[private]
dev-watch-build:
    just _dev-watch ./src "just build"


run: build
    npx serve ./dist

###############################################################################
# Private helpers
###############################################################################

_dev-watch directory command:
    npx nodemon \
        --watch {{directory}} \
        --ext ts,tsx,html,css,json,yaml,yml,txt \
        --exec "{{command}}"

_build-bundle source target:
    echo "Running build-bundle for {{source}} -> {{target}}"
    npx esbuild \
        --preserve-symlinks \
        --loader:.js=jsx \
        --loader:.md=text \
        --loader:.yaml=text \
        --loader:.txt=text \
        --sourcemap \
        --bundle {{source}} \
        --outfile={{target}}
    echo "Done running build-bundle for {{source}} -> {{target}}"
