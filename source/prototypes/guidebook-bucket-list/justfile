###############################################################################
# Preamble
###############################################################################

[private]
default:
  @just --list --unsorted

###############################################################################
# Core commands
###############################################################################

# idempotent installation of dependencies & configuration
ensure:
    cp -Rf $MONOREPO_ROOT/.vscode/ .vscode/
    @which mprocs > /dev/null
    @which deployctl > /dev/null
    npm install

# build the project
build: ensure
    just _build-bundle ./src/main.tsx ./dist/main.bundle.js
    cp -f ./src/index.html ./dist/index.html
    echo $(date) > ./dist/build.timestamp

# run the project with hot-reloading on changes
dev: build
    mprocs \
        --names "client,server" \
        "just dev-watch-client" \
        "just dev-watch-server"

[private]
dev-watch-client:
    just _dev-watch ./src "just build"

[private]
dev-watch-server:
    npx nodemon \
        --watch ./src/server.ts \
        --ext ts,tsx,html,css,json,yaml,yml,txt \
        --exec "just dev-watch-server-restart"

[private]
dev-watch-server-restart:
    echo $(date) > ./dist/build.timestamp
    deno -A ./src/server.ts


publish: build
    @echo "Publishing..."
    cd dist && deployctl \
        deploy --project=guidebook-bucket-list --prod \
        https://jsr.io/@std/http/1.0.7/file_server.ts


###############################################################################
# Private helpers
###############################################################################

# Watches source files in "directory" and runs "command" when they change.
_dev-watch directory command:
    npx nodemon \
        --watch {{directory}} \
        --ext ts,tsx,html,css,json,yaml,yml,txt \
        --exec "{{command}}"

_build-bundle source target:
    echo "Running build-bundle for {{source}} -> {{target}}"
    npx esbuild \
        --preserve-symlinks \
        --loader:.js=jsx \
        --loader:.md=text \
        --loader:.yaml=text \
        --loader:.txt=text \
        --sourcemap \
        --bundle {{source}} \
        --outfile={{target}}
    echo "Done running build-bundle for {{source}} -> {{target}}"
