import "../../common/webapp.justfile"

###############################################################################
# Core commands
###############################################################################

_default: _common-default

# idempotent installation of dependencies & configuration
ensure: _common-ensure-webapp

# build the project
build: ensure
    just _common-build-bundle ./src/main.tsx ./dist/main.bundle.js
    cp -f ./src/index.html ./dist/index.html
    echo $(date) > ./dist/build.timestamp

# run the project with hot-reloading on changes
dev:
    mprocs \
        --names "client,server" \
        "just dev-watch-client" \
        "just dev-watch-server"

[private]
dev-watch-client:
    just _common-dev-watch ./src "just build"

[private]
dev-watch-server:
    npx nodemon \
        --watch ./src/server.ts \
        --ext ts,tsx,html,css,json,yaml,yml,txt \
        --exec "just dev-watch-server-restart"

[private]
dev-watch-server-restart:
    echo $(date) > ./dist/build.timestamp
    deno -A ./src/server.ts


# deploy to guidebook-bucket-list.deno.dev
publish: build
    @echo "Publishing..."
    cd dist && deployctl \
        deploy --project=guidebook-bucket-list --prod \
        https://jsr.io/@std/http/1.0.7/file_server.ts



