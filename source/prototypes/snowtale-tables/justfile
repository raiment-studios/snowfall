# Unfortunately environment variables don't seem to support in the import path
import "../../common/webapp.justfile"


###############################################################################
# Core commands
###############################################################################

_default: _common-default

# idempotent installation of dependencies & configuration
ensure: _common-ensure-webapp
    @just _link-folder ./src/snowfall-core $MONOREPO_ROOT/source/lib/snowfall-core/src
    @just _link-folder ./src/snowfall-ui $MONOREPO_ROOT/source/lib/snowfall-ui/src

build: ensure
    just _common-build-bundle ./src/main.tsx ./dist/main.bundle.js
    cp -f ./src/index.html ./dist/index.html

dev:
    mprocs \
        --names "client,server" \
        "just _common-dev-watch ./src 'just build'" \
        "just run"

run: build
    npx serve ./dist


_link-folder target source:
    #!/usr/bin/env bash
    TARGET={{target}}
    SOURCE={{source}}
    sea cprintln "Creating symbolic link {CC5:$TARGET}"
    if [ ! -L $TARGET ] && [ -e $SOURCE ]; then
        ln -s $SOURCE $TARGET
    fi
